package TimerUtils
	import Table
// 
//	Credits: 
//		* Vexorian (original TimerUtils + TimedLoop)
//		* Nestharus (orignial Constant Timer Loop 32)
	
	// basic TimerUtils functions with Timer-Pool and setData/getData functions
		
	timer array freeTimers
	int freeTimersCount = 0
	Table timerData = new Table()
	
	public function getTimer() returns timer
		if freeTimersCount > 0
			freeTimersCount -= 1
			return freeTimers[freeTimersCount]
		else
			return CreateTimer()
	
	public function timer.setData(integer data) returns timer
		timerData.save( GetHandleId(this), data )
		return this
		
	
	public function timer.getData() returns integer
		return timerData.loadInt( GetHandleId(this) )

	
	public function timer.release()
		this.pause()
		freeTimers[freeTimersCount] = this
		freeTimersCount++
	
	public function timer.pause()
		PauseTimer(this)
		
	public function timer.resume()
		ResumeTimer(this)
	
	public function timer.start(real time, code timerCallBack) returns timer
		TimerStart(this, time, false, timerCallBack)
		return this
		
	public function timer.startPeriodic(real time, code timerCallBack) returns timer
		TimerStart(this, time, true, timerCallBack)
		return this
	
	// module to execute a function after a certain time
	public module Timed
		abstract function onTimer()
		
		private static function timerCallback()
			timer t = GetExpiredTimer()
			thistype c = t.getData() castTo thistype
			c.onTimer()
			t.release()
		
		function startTimer(real time)
			timer t = getTimer()
			t.setData(this castTo integer)
			TimerStart(t, time, false, function timerCallback)
	
	// module to execute a function periodically
	public module Periodic
	
		private timer t
	
		abstract function periodic()
		
		function stopPeriodic()
			t.release()
			t = null
		
		private static function timerCallback()
			timer t = GetExpiredTimer()
			thistype c = t.getData() castTo thistype
			c.periodic()
		
		function startPeriodic(real time)
			t = getTimer()
			t.setData(this castTo integer)
			TimerStart(t, time, true, function timerCallback)
	
	
endpackage


